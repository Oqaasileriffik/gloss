# pre-pipe: zsh -l -c '~/langtech/gloss/kal2dan/kal2dan.pl --gloss'

INCLUDE STATIC ~/langtech/kal/src/cg3/disambiguator.cg3 ;

#TILFØJ MIDLERTIDIGE PERSONTAGS
#For at give oversættelse af person til ledsætninger med Cont
SUBSTITUTE:0000A (Cont) (Cont 1Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r)) (p 1Sg);
SUBSTITUTE:0000B (Cont) (Cont 2Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r)) (p 2Sg);
SUBSTITUTE:0000C (Cont) (Cont 3Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r)) (p 3Sg);
SUBSTITUTE:0000D (Cont) (Cont 1Pl) TARGET CONT IF (0 (/[123][SP][gl]O/r)) (p 1Pl);
SUBSTITUTE:0000E (Cont) (Cont 2Pl) TARGET CONT IF (0 (/[123][SP][gl]O/r)) (p 2Pl);
SUBSTITUTE:0000F (Cont) (Cont 3Pl) TARGET CONT IF (0 (/[123][SP][gl]O/r)) (p 3Pl);

#Når CONT vikarierer for (Cau 2Sg) i paratakse
SUBSTITUTE:0000B (Cont) (Cont 2Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r) + CONJ-LU) (s 2Sg);


#FJERN PERSONTAGS
#Remove tags corresponding to context-fulfilled dependents

AddRelation:Subj (VSTR:{$$34IV}) $$34IV TO (c @SUBJ> OR @<SUBJ) ;
Substitute:Subj (VSTR:{$$34IV}) (VSTR:_x_{$$34IV}) TARGET $$34IV IF (c @SUBJ> OR @<SUBJ);

AddRelation:Obj (VSTR:{$$OBJEKT3}) $$OBJEKT3 TO (c @OBJ> OR @<OBJ) ;
Substitute:Obj (VSTR:{$$OBJEKT3}) (VSTR:_x_{$$OBJEKT3}) TARGET $$OBJEKT3 IF (c @OBJ> OR @<OBJ);

AddRelation:pPerson (VSTR:{$$PERSON}) (V) + $$PERSON (0 Cont OR ContNeg) (NEGATE 0 UTIGE OR RIAR OR GALUAR) (NEGATE 0 @CL-<CIT) TO (p $$PERSON) ;
Substitute:pPerson $$PERSON (VSTR:_x_{$$PERSON}) TARGET (V) IF (0 Cont OR ContNeg) (p $$PERSON) (NEGATE 0 UTIGE OR RIAR OR GALUAR) (NEGATE 0 @CL-<CIT);

AddRelation:CitObj (3SgO) (3SgO) TO (c @CL-<CIT OR @CL-CIT>) ;
SUBSTITUTE:0105 (3SgO) (_x_3SgO) TARGET (3SgO) IF (c @CL-<CIT OR @CL-CIT>);

#Fjern person ved pronominer
SUBSTITUTE:0110A (1Sg) (_x_1Sg) TARGET (1Sg) IF (0 ("uanga") OR ("jeg"));
SUBSTITUTE:0110B (2Sg) (_x_2Sg) TARGET (2Sg) IF (0 ("illit") OR ("dig"));
SUBSTITUTE:0110C (1Pl) (_x_1Pl) TARGET (1Pl) IF (0 ("uanga") OR ("Vi"));
SUBSTITUTE:0110D (2Pl) (_x_2Pl) TARGET (2Pl) IF (0 ("illit") OR ("I"));

#Fjern person ved Gram/Cong
SUBSTITUTE:0120 PN (*) TARGET PN IF (0 ("kisi") OR Gram/Cong);
SUBSTITUTE:0120 PN (*) TARGET $$PN IF (c ("kisi") OR Gram/Cong + $$PN);

#Fjern person ved leksikaliseringer
SUBSTITUTE:0130 (3Sg) (_x_3Sg) TARGET ("taama" IP GALUAR Par 3Sg) OR ("selvom");

#FJERN KASUSTAGS
#Fjern Aeq hvis parent er "like"
SUBSTITUTE:0201 (Aeq) (_x_Aeq) TARGET (Aeq) IF (p* ("som")); #Soorlu UUMASUTUT IKILERNEQARSIMASUTUT, piginnitsini malittariinnarlugu, qungatsini nakkartillugu, tamatigullu maleruutiinnartutut isikkoqarluni.

#Fjern Via hvis leksikaliseret Via
SUBSTITUTE:0205 (Via) (_x_Via) TARGET (Via) IF (0 ("tamaq" Via) OR ("altid" Via)); #Soorlu uumasutut ikilerneqarsimasutut, piginnitsini malittariinnarlugu, qungatsini nakkartillugu, TAMATIGULLU maleruutiinnartutut isikkoqarluni.

#Fjern Abl hvis sammenligning
SUBSTITUTE:0210 (Abl) ("end") TARGET (Abl) IF (0 Gram/2sml); #UANNIIT angajulliunerugaluaqisoq nuannarigakku ukiui apeqqutaatinngilakka.

#Fjern Trm ved @OBJ-DOTR>
SUBSTITUTE:0215 (Trm) (_x_Trm) TARGET (Trm) IF (0 (@OBJ-DOTR>)); #Namminermi meeraaninni aqqusaagarisarsimasakka MEEQQANNUT misigitikkusunngeqigakkit.

#POSSESSIVER
#Oversættelse af "nammineq" som @POSS>
SUBSTITUTE:0300 (N) (N "own") TARGET N + POSSESSUM IF (c ("nammineq" @POSS>) OR ("sig selv" @POSS>));
SUBSTITUTE:0301 ("nammineq" @POSS>) OR ("sig selv" @POSS>) (*) TARGET ("nammineq") OR ("sig selv") IF (p ("own"));

#POSS til præposition
SUBSTITUTE:0305 (@POSS> Rel) ("af") TARGET (@POSS> Rel);

#TILFØJ KONJUNKTIONER OG LIGNENDE
#Flyt kun foran @SUBJ>

#RIAR til "efter"
SUBSTITUTE:0400 (RIAR Cont) ("efter") TARGET (RIAR Cont) #Eqqarsaatigereerparalu soraaruma aatsaat TORERSAARIARLUNGA meeqqakka aaniarlugit.
(NEGATE 0 (UTIGE) OR ("mens")); #Matu tikikkakku MAPPERIAATIGALUGU haloortungalu takussanganngisakka naammattoorakkit tupannermik nillerujussuarpunga.
SUBSTITUTE:0401 (RIAR Cau) ("når") TARGET (RIAR Cau); #TAAMAARIARMALLU eqqarsariaammerujussuarpunga.

#GALUAR til "selvom"
SUBSTITUTE:0405A (@SUBJ>) (@SUBJ> "selvom") TARGET (@SUBJ>) IF (-1 BOS OR CLB) (*1 GALUAR - VFIN BARRIER V); #Anaanaga UNGASILLILAALEREERSIMAGALUARLUNI tunginnut kamappasilluni allullattaalermat nalunngilara ajortoqalersoq.
SUBSTITUTE:0405B (GALUAR Par) OR (GALUAR Cont) OR (GALUAR IteCau) ("selvom") TARGET (GALUAR) IF (0 Par OR Cont OR IteCau) (NOT *0 ("selvom")); #uden subjekt
SUBSTITUTE:0405C (GALUAR Par) (*) TARGET (GALUAR Par) IF (c ("selvom"));
SUBSTITUTE:0405D (GALUAR Cont) (*) TARGET (GALUAR Cont) IF (*-1 ("selvom"));
SUBSTITUTE:0405E (GALUAR IteCau) (*) TARGET (GALUAR IteCau) IF (*-1 ("selvom"));

#CL-<CIT til "som"
SUBSTITUTE:0410A (@SUBJ>) (@SUBJ> "som") TARGET (@SUBJ>) IF (p (@CL-<CIT) - NIR); #Kingusiinnaq illoqarfimmi takuneqarsinnaavoq Louisap anaanani PISUTTUAQATIGISARAA.
SUBSTITUTE:0410B (@CL-<CIT) (*) TARGET (@CL-<CIT) IF (c (@SUBJ>) + ("som")) (NOT 0 NIR);
SUBSTITUTE:0410C (@CL-<CIT Par) OR (@CL-<CIT Cont) ("som") TARGET (@CL-<CIT) IF (0 Par - NIR OR Cont - NIR); #uden subjekt
SUBSTITUTE:0411A (@SUBJ>) (@SUBJ> "som") TARGET (@SUBJ>) IF (p (@CL-CIT>) - NIR);
SUBSTITUTE:0411B (@CL-CIT>) (*) TARGET (@CL-CIT>) IF (c (@SUBJ>) + ("som")) (NOT 0 NIR);
SUBSTITUTE:0411C (@CL-CIT> Par) OR (@CL-CIT> Cont) ("som") TARGET (@CL-CIT>) IF (0 Par - NIR OR Cont - NIR); #uden subjekt

#CL-<CIT til "hvis"
SUBSTITUTE:0415A (@SUBJ>) (@SUBJ> "hvis") TARGET (@SUBJ>) IF (p (@CL-<CIT) + NIR - ("su") - ("be what"));
SUBSTITUTE:0415B (@CL-<CIT) (*) TARGET (@CL-<CIT) IF (c (@SUBJ>) + ("hvis")) (0 NIR);
SUBSTITUTE:0415C (NIR @CL-<CIT Cont) (*) TARGET (NIR @CL-<CIT Cont); ##Paasisinnaaneq ajorpara sooq meeqqannut TAAMAASIORTOORTARNERLUNGA.
SUBSTITUTE:0415D (NIR) (*) TARGET (NIR);

#PRED2 til "når"
SUBSTITUTE:0420A (@SUBJ>) (@SUBJ> "når") TARGET (@SUBJ>) IF (p (@PRED2) + Par); #Anaanani UNITTORLU unilluni, aallartorlu aallarlunilu.
SUBSTITUTE:0420B (@PRED2) (*) TARGET (@PRED2) IF (c (@SUBJ>) + ("når"));
SUBSTITUTE:0420C (@PRED2) ("når") TARGET (@PRED2) + Par IF (NEGATE c (@SUBJ>)); #Anaanani unittorlu unilluni, AALLARTORLU aallarlunilu.

#UTIGE til "mens"
SUBSTITUTE:0425A (@SUBJ>) (@SUBJ> "mens") TARGET (@SUBJ>) IF (-1 BOS OR CLB) (p* UTIGE BARRIER V);
SUBSTITUTE:0425B (UTIGE Cont) ("mens") TARGET (UTIGE Cont); #uden subjekt
SUBSTITUTE:0425C (UTIGE) (*) TARGET (UTIGE) IF (c* ("mens"));

#NIQ Ins og NIQ Abl til "fordi"
SUBSTITUTE:0430 (NIQ Ins) ("fordi") TARGET (NIQ Ins);
SUBSTITUTE:0431 (NIQ Abl) ("fordi") TARGET (NIQ Abl);

#TIGE til "som"
SUBSTITUTE:0435 (TIGE) ("som") TARGET (TIGE);
SUBSTITUTE:0436 (Aeq) ("som") TARGET (Aeq) IF (p* ("som")); #Kiinnakkut APORTINNERUJUSSUARTUT sakkortutigisumik annernertut, takulerpakka anaanaga uigalu tutittut.

#OVERSÆT MODI
#Cond til "når/hvis"
SUBSTITUTE:0450A (@SUBJ>) (@SUBJ> "når/hvis") TARGET (@SUBJ>) IF (*1 Con BARRIER V);
SUBSTITUTE:0450B (Con) (_x_Con) TARGET (Con) IF (*-1 (@SUBJ>) + ("når/hvis"));
SUBSTITUTE:0450C (Con) ("når/hvis") TARGET (Con); #uden subjekt

#Cau til "når/fordi"
SUBSTITUTE:0455A (@SUBJ>) (@SUBJ> "når/fordi") TARGET (@SUBJ>) IF (-1 BOS OR CLB) (*1 Cau BARRIER V);
SUBSTITUTE:0455B (Cau) (_x_Cau) TARGET (Cau) IF (*-1 ("når/fordi") BARRIER V);
SUBSTITUTE:0455C (Cau) ("når/fordi") TARGET (Cau); #uden subjekt

#IteCau til "hver gang"
SUBSTITUTE:0460A (@SUBJ>) (@SUBJ> "hver gang") TARGET (@SUBJ>) IF (*1 IteCau BARRIER V);
SUBSTITUTE:0460B (IteCau) (_x_IteCau) TARGET (IteCau) IF (*-1 (@SUBJ>) + ("hver gang"));
SUBSTITUTE:0460C (IteCau) ("hver gang") TARGET (IteCau); #uden subjekt

#De to følgende skaber forvirrede glosseringer, som vurderes ikke at hjælpe nogen. Cont og ContNeg må i stedet promoveres til ing-former i post-processeringen
#ContNeg til "gør ikke"
#SUBSTITUTE:0030 (ContNeg) ("gør ikke") TARGET (ContNeg);

#Cont til "gør" (midlertidig løsning)
#SUBSTITUTE:0031 (Cont) ("gør") TARGET (Cont) IF (NEGATE 0 @CL-<CIT);

#FJERN ENKLITIKA
SUBSTITUTE:0500 (CONJ-LU) (_x_CONJ-LU) TARGET ("asu") OR ("even"); #asulu er leksikaliseret

#REFLEKSIVER
#Oversættelse af Gram/Refl
SUBSTITUTE:0600A (Gram/Refl) ("mig selv") TARGET (Gram/Refl) + 1Sg;
SUBSTITUTE:0600B (Gram/Refl) ("dig selv") TARGET (Gram/Refl) + 2Sg;
SUBSTITUTE:0600C (Gram/Refl) ("ham/hun/den/det selv") TARGET (Gram/Refl) + 3Sg OR (Gram/Refl) + 4Sg;
SUBSTITUTE:0600D (Gram/Refl) ("os selv") TARGET (Gram/Refl) + 1Pl;
SUBSTITUTE:0600E (Gram/Refl) ("jer selv") TARGET (Gram/Refl) + 2Pl;
SUBSTITUTE:0600F (Gram/Refl) ("dem selv") TARGET (Gram/Refl) + 3Pl OR (Gram/Refl) + 4Pl;

#ANDRE REGLER
#Flytning af enklitika ved hybrider
SUBSTITUTE:0700A (Hyb/1-2) (Hyb/1-2 CONJ-LU) TARGET (Hyb/1-2) IF (1 Hyb/2-2 + CONJ-LU);
SUBSTITUTE:0700B (CONJ-LU) (_x_CONJ-LU) TARGET (Hyb/2-2) IF (0 Hyb/2-2);
SUBSTITUTE:0701A (Hyb/1-2) (Hyb/1-2 CONJ-LI) TARGET (Hyb/1-2) IF (1 Hyb/2-2 + CONJ-LI);
SUBSTITUTE:0701B (CONJ-LI) (_x_CONJ-LI) TARGET (Hyb/2-2) IF (0 Hyb/2-2);
SUBSTITUTE:0702A (Hyb/1-2) (Hyb/1-2 CONJ-LUUNNIIT) TARGET (Hyb/1-2) IF (1 Hyb/2-2 + CONJ-LUUNNIIT);
SUBSTITUTE:0702B (CONJ-LUUNNIIT) (_x_CONJ-LUUNNIIT) TARGET (Hyb/2-2) IF (0 Hyb/2-2);

#Oversættelse af mådesadverbialer (midlertidig løsning)
SUBSTITUTE:0705 (TUQ Ins Sg @ADVL>) OR (TUQ Ins Sg @i-ADVL>) OR ("en som" Ins Sg @ADVL>) OR ("en som" Ins Sg @i-ADVL>) OR ("den som" Ins Sg @ADVL>) OR ("den som" Ins Sg @i-ADVL>) ("-lig") TARGET (TUQ Ins Sg) OR ("en som" Ins Sg) OR ("den som" Ins Sg);
SUBSTITUTE:0706A (Ins Sg @ADVL>) ("-lig") TARGET ("sakkortooq") OR ("stærk");
