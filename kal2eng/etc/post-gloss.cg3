INCLUDE STATIC ~/langtech/kal/src/cg3/disambiguator.cg3 ;

#~/langtech/kal/tools/shellscripts/kal-tokenise ~/langtech/kal/tools/tokenisers/tokeniser-disamb-gt-desc.pmhfst | /usr/local/bin/vislcg3 --grammar ~/langtech/kal/src/cg3/kal-pre1.cg3 | ~/langtech/kal/tools/shellscripts/kal-hybrid-split | /usr/local/bin/vislcg3 --grammar ~/langtech/kal/src/cg3/kal-pre2.cg3 | /usr/local/bin/vislcg3 --grammar ~/langtech/kal/src/cg3/disambiguator.cg3 | /usr/local/bin/vislcg3 --grammar ~/langtech/kal/src/cg3/functions.cg3 | /usr/local/bin/vislcg3 --grammar ~/langtech/kal/src/cg3/dependency.cg3 #det virker med det her?

#HUSK AT BRUGE BÅDE GRØNLANDSKE OG ENGELSKE LEKSEMER FOR REGLER BASERENDE PÅ LEKSEMER FRA KATERSAT (de er ikke oversat her, men allerede i Inspect)

#TILFØJ MIDLERTIDIGE PERSONTAGS
#For at give oversættelse af person til ledsætninger med Cont
SUBSTITUTE:0000A (Cont) (Cont 1Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r))(p 1Sg);
SUBSTITUTE:0000B (Cont) (Cont 2Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r))(p 2Sg);
SUBSTITUTE:0000C (Cont) (Cont 3Sg) TARGET CONT IF (0 (/[123][SP][gl]O/r))(p 3Sg);
SUBSTITUTE:0000D (Cont) (Cont 1Pl) TARGET CONT IF (0 (/[123][SP][gl]O/r))(p 1Pl);
SUBSTITUTE:0000E (Cont) (Cont 2Pl) TARGET CONT IF (0 (/[123][SP][gl]O/r))(p 2Pl);
SUBSTITUTE:0000F (Cont) (Cont 3Pl) TARGET CONT IF (0 (/[123][SP][gl]O/r))(p 3Pl);

#FJERN PERSONTAGS
#Remove tags corresponding to context-fulfilled dependents
SUBSTITUTE:0100A (3Sg) (*) TARGET (3Sg) IF (c @SUBJ> OR @<SUBJ);
SUBSTITUTE:0100B (3Pl) (*) TARGET (3Pl) IF (c @SUBJ> OR @<SUBJ);
SUBSTITUTE:0101A (4Sg) (*) TARGET (4Sg) IF (c @SUBJ> OR @<SUBJ);
SUBSTITUTE:0101B (4Pl) (*) TARGET (4Pl) IF (c @SUBJ> OR @<SUBJ);
SUBSTITUTE:0102A (3SgO) (*) TARGET (3SgO) IF (c @OBJ> OR @<OBJ);
SUBSTITUTE:0102B (3PlO) (*) TARGET (3PlO) IF (c @OBJ> OR @<OBJ);
SUBSTITUTE:0103A (4Sg) (*) TARGET (4Sg) IF (0 Cont OR ContNeg)(p V);
SUBSTITUTE:0103B (4Pl) (*) TARGET (4Pl) IF (0 Cont OR ContNeg)(p V);
SUBSTITUTE:0104 $$PERSON (*) TARGET (V) IF (0 Cont OR ContNeg)(p $$PERSON)(NEGATE 0 @CL-<CIT);
SUBSTITUTE:0105 (3SgO) (*) TARGET (3SgO) IF (c @CL-<CIT OR @CL-CIT>);

#Fjern person ved pronominer
SUBSTITUTE:0110A (1Sg) (*) TARGET (1Sg) IF (0 ("uanga") OR ("I"));
SUBSTITUTE:0110B (2Sg) (*) TARGET (2Sg) IF (0 ("illit") OR ("you"));
SUBSTITUTE:0110C (1Pl) (*) TARGET (1Pl) IF (0 ("uanga") OR ("we"));
SUBSTITUTE:0110D (2Pl) (*) TARGET (2Pl) IF (0 ("illit") OR ("you"));

#Fjern person ved Gram/Cong
SUBSTITUTE:0120 PN (*) TARGET PN IF (0 ("kisi") OR Gram/Cong);
SUBSTITUTE:0120 PN (*) TARGET $$PN IF (c ("kisi") OR Gram/Cong + $$PN);

#FJERN KASUSTAGS
#Fjern Aeq hvis parent er "like"
SUBSTITUTE:0200 (Aeq) (*) TARGET (Aeq) IF (p* ("like")); #Soorlu UUMASUTUT IKILERNEQARSIMASUTUT, piginnitsini malittariinnarlugu, qungatsini nakkartillugu, tamatigullu maleruutiinnartutut isikkoqarluni.

#Fjern Via hvis leksikaliseret Via
SUBSTITUTE:0205 (Via) (*) TARGET (Via) IF (0 ("tamaq" Via) OR ("always" Via)); #Soorlu uumasutut ikilerneqarsimasutut, piginnitsini malittariinnarlugu, qungatsini nakkartillugu, TAMATIGULLU maleruutiinnartutut isikkoqarluni.

#Fjern Abl hvis sammenligning
SUBSTITUTE:0210 (Abl) ("than") TARGET (Abl) IF (0 Gram/2sml); #UANNIIT angajulliunerugaluaqisoq nuannarigakku ukiui apeqqutaatinngilakka.

#Fjern Trm ved @OBJ-DOTR>
SUBSTITUTE:0215 (Trm) (*) TARGET (Trm) IF (0 (@OBJ-DOTR>)); #Namminermi meeraaninni aqqusaagarisarsimasakka MEEQQANNUT misigitikkusunngeqigakkit.

#POSSESSIVER
#Oversættelse af "nammineq" som @POSS>
SUBSTITUTE:0300 (N) (N "own") TARGET N + POSSESSUM IF (c ("nammineq" @POSS>) OR ("oneself" @POSS>));
SUBSTITUTE:0301 ("nammineq" @POSS>) OR ("oneself" @POSS>) (*) TARGET ("nammineq") OR ("oneself") IF (p ("own"));

#POSS til præposition
SUBSTITUTE:0305 (@POSS> Rel) ("of") TARGET (@POSS> Rel);

#TILFØJ KONJUNKTIONER OG LIGNENDE
#Rækkefølge efter sandsynlighed: Først @ADVL>, så @SUBJ>, så @OBJ>, så selve verbum, så fjernelse af morfem

#GALUAR til "even though"
SUBSTITUTE:0400A (@ADVL>) (@ADVL> "even though") TARGET (@ADVL>) IF (-1 BOS OR CLB)(p* GALUAR); #Uanniit ANGAJULLIUNERUGALUAQISOQ nuannarigakku ukiui apeqqutaatinngilakka.
SUBSTITUTE:0400B (@SUBJ>) (@SUBJ> "even though") TARGET (@SUBJ>) IF (-1 BOS OR CLB)(*1 GALUAR BARRIER V); #Anaanaga UNGASILLILAALEREERSIMAGALUARLUNI tunginnut kamappasilluni allullattaalermat nalunngilara ajortoqalersoq.
SUBSTITUTE:0400C (GALUAR Par) OR (GALUAR Cont) OR (GALUAR IteCau) ("even though") TARGET (GALUAR) IF (NOT *0 ("even though")); #uden subjekt
SUBSTITUTE:0400C (GALUAR Par) (*) TARGET (GALUAR Par) IF (c ("even though"));
SUBSTITUTE:0400D (GALUAR Cont) (*) TARGET (GALUAR Cont) IF (*-1 ("even though"));
SUBSTITUTE:0400E (GALUAR IteCau) (*) TARGET (GALUAR IteCau) IF (*-1 ("even though"));

#CL-<CIT til "that"
SUBSTITUTE:0405A (@SUBJ>) (@SUBJ> "that") TARGET (@SUBJ>) IF (p (@CL-<CIT) - NIR); #Kingusiinnaq illoqarfimmi takuneqarsinnaavoq Louisap anaanani PISUTTUAQATIGISARAA.
SUBSTITUTE:0405B (@CL-<CIT) (*) TARGET (@CL-<CIT) IF (c (@SUBJ>) + ("that"))(NOT 0 NIR);
SUBSTITUTE:0405C (@CL-<CIT Par) OR (@CL-<CIT Cont) ("that") TARGET (@CL-<CIT) IF (0 Par - NIR OR Cont - NIR); #uden subjekt

#CL-<CIT til "if"
SUBSTITUTE:0410A (@SUBJ>) (@SUBJ> "if") TARGET (@SUBJ>) IF (p (@CL-<CIT) + NIR - ("su") - ("be what"));
SUBSTITUTE:0410B (@CL-<CIT) (*) TARGET (@CL-<CIT) IF (c (@SUBJ>) + ("if"))(0 NIR);
SUBSTITUTE:0410C (NIR @CL-<CIT Cont) (*) TARGET (NIR @CL-<CIT Cont); ##Paasisinnaaneq ajorpara sooq meeqqannut TAAMAASIORTOORTARNERLUNGA.
SUBSTITUTE:0410D (NIR) (*) TARGET (NIR);

#PRED2 til "when"
SUBSTITUTE:0415A (@SUBJ>) (@SUBJ> "when") TARGET (@SUBJ>) IF (p (@PRED2) + Par); #Anaanani UNITTORLU unilluni, aallartorlu aallarlunilu.
SUBSTITUTE:0415B (@PRED2) (*) TARGET (@PRED2) IF (c (@SUBJ>) + ("when"));
SUBSTITUTE:0415C (@PRED2) ("when") TARGET (@PRED2) + Par IF (NEGATE c (@SUBJ>)); #Anaanani unittorlu unilluni, AALLARTORLU aallarlunilu.

#UTIGE til "while"
SUBSTITUTE:0420A (@ADVL>) (@ADVL> "while") TARGET (@ADVL>) IF (-1 BOS OR CLB)(p* UTIGE); #Nunaannarmut NAKKUSSIUTIGALUNI, kavaajami paffequtaa kimmarlugu oqummiartuartarpaa, isigisani iluamik paaserpasissanagu.
SUBSTITUTE:0420B (@SUBJ>) (@SUBJ> "while") TARGET (@SUBJ>) IF (-1 BOS OR CLB)(p* UTIGE);
SUBSTITUTE:0420C (@OBJ>) (@OBJ> "while") TARGET (@OBJ>) IF (-1 BOS OR CLB)(p* UTIGE); #Anaanaga saannut pillunilu, sakissakkut tigullunga kiviutigalunga, KAVAAJARA nusoqattaajutigalugu issuttuullunga aallartippaanga.
SUBSTITUTE:0420D (UTIGE) (*) TARGET (UTIGE) IF (c* ("while"));

#Cond til "when/if"
SUBSTITUTE:0425A (@SUBJ>) (@SUBJ> "when/if") TARGET (@SUBJ>) IF (*1 Con BARRIER V);
SUBSTITUTE:0425B (Con) (*) TARGET (Con) IF (*-1 (@SUBJ>) + ("when/if"));
SUBSTITUTE:0425C (Con) ("when/if") TARGET (Con); #uden subjekt

#Cau til "when/because"
SUBSTITUTE:0430A (@ADVL>) (@ADVL> "when/because") TARGET (@ADVL>) IF (-1 BOS OR CLB)(*1 Cau BARRIER V); #NAMMINEERLUTA angerlarsimaffeqaleratta meeqqiorluta aallartippugut.
SUBSTITUTE:0430B (@SUBJ>) (@SUBJ> "when/because") TARGET (@SUBJ>) IF (-1 BOS OR CLB)(*1 Cau BARRIER V);
SUBSTITUTE:0430B (Cau) (*) TARGET (Cau) IF (*-1 ("when/because"));
SUBSTITUTE:0430C (Cau) ("when/because") TARGET (Cau); #uden subjekt

#IteCau til "everytime"
SUBSTITUTE:0435A (@SUBJ>) (@SUBJ> "everytime") TARGET (@SUBJ>) IF (*1 IteCau BARRIER V);
SUBSTITUTE:0435B (IteCau) (*) TARGET (IteCau) IF (*-1 (@SUBJ>) + ("everytime"));
SUBSTITUTE:0435C (IteCau) ("everytime") TARGET (IteCau); #uden subjekt

#ContNeg til "not doing"
SUBSTITUTE:0440 (ContNeg) ("not doing") TARGET (ContNeg);

#Cont til "doing" (midlertidig løsning)
SUBSTITUTE:0445 (Cont) ("doing") TARGET (Cont) IF (NEGATE 0 @CL-<CIT);

#RIAR til "after"
SUBSTITUTE:0450 (RIAR) ("after") TARGET (RIAR); #Eqqarsaatigereerparalu soraaruma aatsaat TORERSAARIARLUNGA meeqqakka aaniarlugit.


#ANDRE REGLER
#Flytning af enklitika ved hybrider
SUBSTITUTE:0500A (Hyb/1-2) (Hyb/1-2 CONJ-LU) TARGET (Hyb/1-2) IF (1 Hyb/2-2 + CONJ-LU);
SUBSTITUTE:0500B (CONJ-LU) (*) TARGET (Hyb/2-2) IF (0 Hyb/2-2);
SUBSTITUTE:0501A (Hyb/1-2) (Hyb/1-2 CONJ-LI) TARGET (Hyb/1-2) IF (1 Hyb/2-2 + CONJ-LI);
SUBSTITUTE:0501B (CONJ-LI) (*) TARGET (Hyb/2-2) IF (0 Hyb/2-2);
SUBSTITUTE:0502A (Hyb/1-2) (Hyb/1-2 CONJ-LUUNNIIT) TARGET (Hyb/1-2) IF (1 Hyb/2-2 + CONJ-LUUNNIIT);
SUBSTITUTE:0502B (CONJ-LUUNNIIT) (*) TARGET (Hyb/2-2) IF (0 Hyb/2-2);

#Oversættelse af mådesadverbialer (midlertidig løsning)
SUBSTITUTE:0505 (TUQ Ins Sg @ADVL>) OR (TUQ Ins Sg @i-ADVL>) ("-ly") TARGET (TUQ Ins Sg);
SUBSTITUTE:0506A (Ins Sg @ADVL>) ("-ly") TARGET ("sakkortooq") OR ("strong");

#Oversættelse af Gram/Refl
SUBSTITUTE:0510A (Gram/Refl) ("myself") TARGET (Gram/Refl) + 1Sg;
SUBSTITUTE:0510B (Gram/Refl) ("yourself") TARGET (Gram/Refl) + 2Sg;
SUBSTITUTE:0510C (Gram/Refl) ("himself/herself/itself") TARGET (Gram/Refl) + 3Sg OR (Gram/Refl) + 4Sg;
SUBSTITUTE:0510D (Gram/Refl) ("ourselves") TARGET (Gram/Refl) + 1Pl;
SUBSTITUTE:0510E (Gram/Refl) ("yourselves") TARGET (Gram/Refl) + 2Pl;
SUBSTITUTE:0510C (Gram/Refl) ("themselves") TARGET (Gram/Refl) + 3Pl OR (Gram/Refl) + 4Pl;